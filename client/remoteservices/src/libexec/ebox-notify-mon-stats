#!/usr/bin/perl

# Copyright (C) 2009 eBox Technologies S.L.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License, version 2, as
# published by the Free Software Foundation.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

# Description:
#
#  This script is intended to notify CC with new monitoring stats. It
#  will send a tarball containing the whole monitoring stats the first
#  time, if there is already a tarball, a delta is sent using *xdelta*
#  tool

use Archive::Tar;

use EBox;
use EBox::Config;
use EBox::Global;
use EBox::Exceptions::Command;
use EBox::Monitor::Configuration;
use EBox::RemoteServices::Configuration;
use EBox::RemoteServices::Monitor;
use EBox::Sudo;

use Error qw(:try);
use File::Temp;
use Getopt::Long;
use Pod::Usage;

use constant MON_CRON_INTERVAL => 15 * 60;

my @fileList = ();
my ($usage, $force) = (0,0);
my $correct = GetOptions(
    "usage|help" => \$usage,
    "force"      => \$force,
   );

if ( $usage or (not $correct)) {
    pod2usage(1);
}

EBox::init();

my $rs = EBox::Global->modInstance('remoteservices');
unless ( $rs->eBoxSubscribed() ) {
    exit;
}
my $mon = EBox::Global->modInstance('monitor');
unless ( $mon->isEnabled() ) {
    open(my $fd, '<', EBox::RemoteServices::Configuration::MonTimestamp());
    my $lastTime = <$fd>;
    close($fd);
    if ( $lastTime + MON_CRON_INTERVAL < time() ) {
        exit;
    }
}

my $rrdDir = EBox::Monitor::Configuration::RRDBaseDirPath();

# Stop collectd prior to creating the tar
$mon->stopService();
my $currentTar = new File::Temp(DIR => EBox::Config::tmp());
my $tempDir = File::Temp->newdir(DIR => EBox::Config::tmp());
try {
    EBox::Sudo::command(
        '/bin/tar cf ' . $currentTar->filename() . " -C $rrdDir ."
       );
} catch EBox::Exceptions::Command with {
    my ($exc) = @_;
    EBox::error($exc);
};
$mon->restartService();

my $monSrv = new EBox::RemoteServices::Monitor();
my $retValue = 1;
my $oldTar = EBox::RemoteServices::Configuration::OldTarLocation();
if ( (not $force) and -r $oldTar and -f $oldTar ) {
    my $deltaFile = new File::Temp(DIR => EBox::Config::tmp());
    EBox::Sudo::command(
        "/usr/bin/xdelta3 -fe -s $oldTar " . $currentTar->filename() . ' ' . $deltaFile->filename()
       );
    try {
        $retValue = $monSrv->sendDelta($deltaFile->filename());
    } catch EBox::Exceptions::Base with {
        # If anything go wrong with the sending delta, send them all
        $retValue = $monSrv->sendAll($currentTar->filename());
    };
} else {
    $retValue = $monSrv->sendAll($currentTar->filename());
}

if ($retValue) {
    EBox::Sudo::command(
        '/bin/mv ' . $currentTar->filename() . " $oldTar"
       );
    # Set the monitoring timestamp
    open(my $fd, '>', EBox::RemoteServices::Configuration::MonTimestamp());
    print $fd time() . "\n";
    close($fd);
}

1;

__END__

=head1 NAME

ebox-notify-mon-stats - Notify monitoring stats to CC

=head1 SYNOPSIS

ebox-notify-mon-stats [--usage|help] [--force]

 It will send a tar containing the monitoring stats to CC. A delta file
 using xdelta3 tool if there is already a tar file created previously

 Options:
   --usage|help  Print this help and exit

   --force       Send the whole tar file even though you already have
                 a delta

=cut

