<%doc>
HTML table component
Parameters:
  @columnTitles         - The titles of the columns
  @rows                 - The rows from the table. Each row is a array
  reference which contains their elements
  @additionalComponents - Additional mason components to add to the
  table. Remember to wrap elements between  <tr> and </tr> if you want the
  component inside of the table's rows
</%doc>
<%args>
@columnTitles => ()
@rows         => ()
@additionalComponents => ()
</%args>
<%attr>
 tableClass => undef
 theadClass => undef
 thClass    => undef
 tbodyClass => undef
 trClass    => undef
 tdClass    => undef
</%attr>
<%init>
sub _class { my ($class) = @_; defined $class ?  "class='$class'" : "" }
</%init>
<& SELF:table, columnTitles => \@columnTitles, rows => \@rows,
additionalComponents => \@additionalComponents &>


<%method table>
<%args>
@columnTitles => ()
@rows
@additionalComponents => ()
</%args>
<%init>
my $tableClass =  $m->base_comp->attr('tableClass');
</%init>
<div class="table-wrapper">
<table <% _class($tableClass) %> >
  <& SELF:thead, columnTitles => \@columnTitles &>
  <& SELF:tbody, rows => \@rows, additionalComponents =>
  \@additionalComponents &>
</table>
</div>
</%method>

<%method thead>
<%args>
@columnTitles => ()
</%args>
<%init>
my $theadClass =  $m->base_comp->attr('theadClass');
</%init>
 <thead <% _class($theadClass)  %> >
     <& SELF:theadContent, columnTitles => \@columnTitles &>
 </thead>
</%method>


<%method theadContent>
<%args>
@columnTitles => ()
</%args>
<%init>
my $thClass =  $m->base_comp->attr('thClass');
</%init>
%   foreach my $title (@columnTitles) {
    <& SELF:th, text => $title, thClass => $thClass &>
%   }
</%method>

<%method th>
<%args>
$text
$thClass => undef
</%args>
 <th <% _class($thClass) %> >
  <% $text %>
 </th>
</%method>

<%method tbody>
<%args>
@rows
@additionalComponents => ()
</%args>
<%init>
my $tbodyClass =  $m->base_comp->attr('tbodyClass');
</%init>
 <tbody <% _class($tbodyClass) %> >
     <& SELF:rows, rows => \@rows &>
     <& SELF:additionalComponents, components => \@additionalComponents &>
 </tbody>
</%method>

<%method rows>
<%args>
@rows
</%args>
% foreach my $row_r (@rows) {
    <& SELF:tr, row => $row_r &>
% }
</%method>

<%method tr>
<%args>
@row
</%args>
<%init>
my $trClass =  $m->base_comp->attr('trClass');
</%init>
<tr <% _class($trClass) %>>
<& SELF:trContent, row => \@row &>
</tr>
</%method>

<%method trContent>
<%args>
@row
</%args>
<%init>
my $tdClass =  $m->base_comp->attr('tdClass');
</%init>
%   foreach my $element ( @row ) {
       <& SELF:td, element => $element, tdClass => $tdClass &>
%    }
</%method>

<%method td>
<%args>
$element
$tdClass => undef
</%args>
<td <% _class($tdClass) %>> <% $element %>  </td>
</%method>

<%method additionalComponents>
<%args>
@components
</%args>
% return if (@components == 0);
<& /componentCall.mas, @components &>
</%method>
