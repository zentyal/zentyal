#!/usr/bin/perl

# Copyright (C) 2012-2013 Zentyal S.L.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License, version 2, as
# published by the Free Software Foundation.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

use strict;
use warnings;

use EBox;
use EBox::Config;
use EBox::Global;
use EBox::Users::Group;
use EBox::Users::User;

use TryCatch::Lite;
use File::Slurp;
use Time::gmtime;

my %ignoreOUs = map {( $_ => $_)} qw(Users Groups Computers Builtin Kerberos postfix zarafa Extensions Queues);
my @ignoreChildrenOUs = qw(postfix Kerberos zarafa Extensions);

sub getTimestamp
{
    my $time = gmtime();
    my $timestamp = sprintf("%04d%02d%02d%02d%02d%02d",
        $time->year + 1900, $time->mon + 1, $time->mday,
        $time->hour, $time->min, $time->sec);
    return $timestamp;
}

sub ignoreOU
{
    my ($ouName) = @_;

    return ((exists $ignoreOUs{$ouName}) or
            (grep { $ouName =~ /^$_\// } @ignoreChildrenOUs));
}

EBox::init();

my $sambaTimestampFile = EBox::Config::home() . '.s4sync_ts';
unless (-f $sambaTimestampFile) {
    write_file($sambaTimestampFile, '0');
}

my $ro = 1;
my $global = EBox::Global->getInstance($ro);
my $sambaModule = $global->modInstance('samba');
my $usersModule = $global->modInstance('users');

my $ldb = $sambaModule->ldb();
while (1) {
    # Sleeping at the loop start gives time to samba daemon to fully start
    sleep (30);
    next unless ($sambaModule->isEnabled() and $sambaModule->getProvision->isProvisioned());

    # The Administrator user, 'Domain Users' and 'Domain admins' groups are special case entries.
    # Those are Zentyal system user and groups so don't appear by default on list actions.
    my $domainSID = $sambaModule->ldb()->domainSID();
    my $domainAdminSID = "$domainSID-500";
    my $domainUsersSID = "$domainSID-513";
    my $domainAdminsSID = "$domainSID-512";

    my $sambaTimestamp = read_file($sambaTimestampFile);

    my %sambaUsers = map { $_->get('samAccountName') => $_ } @{$ldb->users()};
    my %sambaContacts = map { $_->canonicalName(1) => $_ } @{$ldb->contacts()};
    my %sambaGroups = map { $_->get('samAccountName') => $_ } @{$ldb->groups()};
    my @orderSambaOUs;
    my %sambaOUs;
    foreach my $ou (@{$ldb->ous()}) {
        my $canonicalName = $ou->canonicalName(1);
        if (ignoreOU($canonicalName)) {
            next;
        }
        push @orderSambaOUs, $canonicalName;
        $sambaOUs{$canonicalName} = $ou;
    }

    write_file($sambaTimestampFile, getTimestamp());

    my %zentyalUsers = map { $_->get('uid') => $_ } @{$usersModule->users()};
    my %zentyalContacts = map { $_->canonicalName(1) => $_ } @{$usersModule->contacts()};
    my %zentyalGroups = map { $_->get('cn') => $_ } @{$usersModule->groups()};
    my %zentyalOUs    = map { $_->canonicalName(1) => $_ } @{$usersModule->ous()};

    foreach my $canonicalName (@orderSambaOUs) {
        if (exists $zentyalOUs{$canonicalName}) {
            my $changed = $sambaOUs{$canonicalName}->get('whenChanged');
            $changed =~ s/\.\d+Z//;
            if ($changed > $sambaTimestamp) {
                try {
                    $sambaOUs{$canonicalName}->updateZentyal();
                } catch {
                    my ($error) = @_;
                    EBox::error("Error updating OU $canonicalName: $error. s4sync will try again in next synchronization");
                }
            }
            delete $zentyalOUs{$canonicalName};
        } else {
            try {
                $sambaOUs{$canonicalName}->addToZentyal();
            } catch {
                my ($error) = @_;
                EBox::error("Error adding OU $canonicalName: $error. s4sync will try again in next synchronization");
            }
        }
    }

    my @orderZentyalOus = reverse sort keys %zentyalOUs;
    foreach my $ouCanonicalName (reverse sort keys %zentyalOUs) {
        if (ignoreOU($ouCanonicalName)) {
            next;
        }
        EBox::debug("Deleting Zentyal OU '" . $zentyalOUs{$ouCanonicalName}->dn() . "'");
        $zentyalOUs{$ouCanonicalName}->setIgnoredModules(['samba']);
        $zentyalOUs{$ouCanonicalName}->deleteObject();
    }

    #################################################
    #### Sync users
    #################################################
    # Add users that exists in samba but not in Zentyal
    foreach my $sambaUserName (sort keys %sambaUsers) {
        if ((exists $zentyalUsers{$sambaUserName}) or
            ($domainAdminSID eq $sambaUsers{$sambaUserName}->sid())) {
            my $changed = $sambaUsers{$sambaUserName}->get('whenChanged');
            $changed =~ s/\.\d+Z//;
            if ($changed > $sambaTimestamp) {
                try {
                    $sambaUsers{$sambaUserName}->updateZentyal();
                } catch {
                    my ($error) = @_;
                    EBox::error("Error updating user $sambaUserName: $error. s4sync will try again in next synchronization");
                }
            }
            delete $zentyalUsers{$sambaUserName};
        } else {
            try {
                $sambaUsers{$sambaUserName}->addToZentyal();
            } catch {
                my ($error) = @_;
                EBox::error("Error adding user $sambaUserName: $error. s4sync will try again in next synchronization");
            }
        }
    }

    foreach my $zentyalUserName (sort keys %zentyalUsers) {
        EBox::info("Deleting Zentyal user '$zentyalUserName'");
        $zentyalUsers{$zentyalUserName}->setIgnoredModules(['samba']);
        $zentyalUsers{$zentyalUserName}->deleteObject();
    }

    #################################################
    #### Sync contacts
    #################################################
    # Add contacts that exists in samba but not in Zentyal
    foreach my $sambaContactCanonicalName (sort keys %sambaContacts) {
        if (exists $zentyalContacts{$sambaContactCanonicalName}) {
            my $changed = $sambaContacts{$sambaContactCanonicalName}->get('whenChanged');
            $changed =~ s/\.\d+Z//;
            if ($changed > $sambaTimestamp) {
                try {
                    $sambaContacts{$sambaContactCanonicalName}->updateZentyal();
                } catch {
                    my ($error) = @_;
                    EBox::error("Error updating contact $sambaContactCanonicalName: $error. s4sync will try again in next synchronization");
                }
            }
            delete $zentyalContacts{$sambaContactCanonicalName};
        } else {
            try {
                $sambaContacts{$sambaContactCanonicalName}->addToZentyal();
            } catch {
                my ($error) = @_;
                EBox::error("Error adding contact $sambaContactCanonicalName: $error. s4sync will try again in next synchronization");
            }
        }
    }

    foreach my $zentyalContactCanonicalName (sort keys %zentyalContacts) {
        EBox::debug("Deleting Zentyal contact '$zentyalContactCanonicalName'");
        $zentyalContacts{$zentyalContactCanonicalName}->setIgnoredModules(['samba']);
        $zentyalContacts{$zentyalContactCanonicalName}->deleteObject();
    }

    #################################################
    #### Sync groups
    #################################################
    foreach my $sambaGroupName (sort keys %sambaGroups) {
        if ((exists $zentyalGroups{$sambaGroupName}) or
            ($domainAdminsSID eq $sambaGroups{$sambaGroupName}->sid()) or
            ($domainUsersSID eq $sambaGroups{$sambaGroupName}->sid())) {
            my $changed = $sambaGroups{$sambaGroupName}->get('whenChanged');
            $changed =~ s/\.\d+Z//;
            if ($changed > $sambaTimestamp) {
                try {
                    $sambaGroups{$sambaGroupName}->updateZentyal();
                } catch {
                    my ($error) = @_;
                    EBox::error("Error updating group $sambaGroupName: $error. s4sync will try again in next synchronization");
                }
            }
            delete $zentyalGroups{$sambaGroupName};
        } else {
            try {
                $sambaGroups{$sambaGroupName}->addToZentyal();
            } catch {
                my ($error) = @_;
                EBox::error("Error adding group $sambaGroupName: $error. s4sync will try again in next synchronization");
            }
        }
    }

    foreach my $zentyalGroupName (sort keys %zentyalGroups) {
        EBox::debug("Deleting Zentyal group '$zentyalGroupName'");
        $zentyalGroups{$zentyalGroupName}->setIgnoredModules(['samba']);
        $zentyalGroups{$zentyalGroupName}->deleteObject();
    }
}
