#!/usr/bin/perl

use strict;
use warnings;


use Getopt::Long;
use Cwd;
use HTML::Mason;
use File::Slurp;
use File::Copy;

use constant 
    {
       MODULE_VERSION => '0.1',
       STUBS_PATH => '@DATADIR@/emoddev/stubs',
    };

sub types
{
    return qw/Text Int Boolean Port HostIP IPAddress/;
}

sub _printValidTypes
{
    for my $type (types()) {
        print STDERR "\t $type\n";
    }
    print STDERR "\n\tExample:\n";
    print STDERR "\t\t --field name:Text --field active:Boolean\n";

}
sub _printUsage
{
    print STDERR "\nUsage: $0 --main-class class --name name --field field1:type1 [Options]\n\n";
    print STDERR "Options:\n";
    print STDERR "\t--model form | table. Default: form\n";
    print STDERR "\t--destdir\n";
    print STDERR "Types:\n";
    _printValidTypes();
    exit 1;
}

sub _parseFields
{
    my ($fields) = @_;

    my %validTypes = map {$_ => 1} types();
    my %names;
    my  @fields;
    for my $pair (@{$fields}) {
        unless ($pair =~ /:/) {
            print STDERR "$pair is not a valid field:type\n";
            exit 1;
        }
        my ($name, $type) = split(/:/, $pair);
        unless (exists $validTypes{$type}) {
            print STDERR "$type from $name is not a valid type\n";
            exit 1;
        }
        if (exists $names{$name}) {
            print STDERR "$name already exists\n";
            exit 1;
        } else {
            $names{$name} = $type;
            push (@fields, { name => $name, type => $type});
        }
    }

    return \@fields;
}

sub _parseOptions
{
    my %options = ( 
            name =>   undef,
            fields => [], 
            destdir => undef,
            model => 'form',
            mainClass => undef,
            );
    my $help;
    my $info;
    my @fields;
    my $optionsOk = GetOptions(
            'main-class=s' => \$options{mainClass},
            'name=s' => \$options{name},
            'fields=s' => $options{fields},
            'destdir=s' => \$options{destdir},
            'model=s' => \$options{model},
            'help'  => \$help,
            'info'  => \$info,
            );
    $options{parsedFields} = _parseFields($options{fields});
    if (not $optionsOk or $info or $help) {
        _printUsage();
    }

    unless (defined($options{name}) and defined($options{fields})) {
        _printUsage();
    }

    if ($options{destdir}) {
        unless (-d $options{destdir}) {
            die "$options{destidir} does not exist";
        }
    } else {
        $options{destdir} = getcwd;
    }

    unless ( -d $options{destdir} ) {
        die "$options{destdir} does not exist!!";
    }

    return \%options;
}

sub _models
{
    my ($dir) = @_;
    my @models ;
    opendir ( my $dirFH, $dir );
    while ( defined ( my $file = readdir ( $dirFH ))) {
          next unless ( $file =~ m/.*\.pm/ );
          push(@models, $file);
    }
    return \@models; 
}
sub _makefile
{
    my ($interp, $output, $options) = @_;

    my $dir = $options->{destdir} . '/src/EBox/Model';

   my $comp = $interp->make_component(
            comp_file => STUBS_PATH .  '/src/Model/Makefile.am.mas',
            );
    ${$output} = '';
    $interp->exec($comp,
                    ( module => $options->{mainClass},
                      models => _models($dir)));

    write_file($dir . '/Makefile.am', ${$output});
}

sub _src_EBox_Model
{
    my ($interp, $output, $options) = @_;

    my $dir = $options->{destdir} . '/src/EBox/Model';
    my $mainClass = $options->{name} . '.pm';
    mkdir ($dir) unless (-d $dir); 

    # TableModel or FormModel ?
    my $template = '/src/Model/FormModel.pm.mas';
   
    my $comp = $interp->make_component(
            comp_file => STUBS_PATH . $template,
            );
    ${$output} = '';
    $interp->exec($comp, 
            ( mainClass => 'EBox::' . $options->{mainClass},
              modelName => $options->{name},
              printableTableName => $options->{name}, 
              rowName   => $options->{name},
              modelDomain => $options->{mainClass},
              fields    => $options->{parsedFields},
              ));
    write_file("$dir/$mainClass", ${$output});

}

sub _addModelsToMainClass
{
    my ($interp, $output, $options) = @_;

    my $mainClass = $options->{mainClass};
    my $file = $options->{destdir} . "/src/EBox/$mainClass.pm";
    my $dir = $options->{destdir} . "/src/EBox/Model";

    my $code = "    return [\n";
    for my $model (@{_models($dir)}) {
        $code .= ' ' x 8;
        $code .= "'EBox::${mainClass}::Model::$model',\n";
    }
    $code .= "    ];\n";

    my $fd;
    open($fd, $file) or die "Can open $file\n";
    my @oldFile = <$fd>;
    close($fd);
    
    open($fd, ">$file");
    my $inFunction = undef;
    for my $line (@oldFile) {
        if ($line =~ /sub modelClasses/) {
            $inFunction = 1;    
        } elsif ($inFunction and $line =~ /{/) {
            print $fd "{\n$code";
            next;
        } elsif ($inFunction and $line =~ /}/) {
            $inFunction = undef
        }  elsif ($inFunction) {
            next;
        }
        print $fd $line;
    }
    close($fd);
}

sub _createModel
{
    # Main
    my $options = _parseOptions();

    my $dir = $options->{destdir};
    
    my $output;
    my $interp = HTML::Mason::Interp->new(comp_root => STUBS_PATH, 
            out_method => \$output);

   _src_EBox_Model($interp, \$output, $options);
   _makefile($interp, \$output, $options);
   _addModelsToMainClass($interp, \$output, $options);

}

_createModel();
