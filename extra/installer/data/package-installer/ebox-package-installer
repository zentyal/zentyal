#!/usr/bin/perl
# Copyright (C) 2009 eBox Technologies S.L.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License, version 2, as
# published by the Free Software Foundation.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

use warnings;
use strict;

use EBox::Gettext;

EBox::Gettext::settextdomain('ebox-package-installer');

#########################
# -- eBox package data --
#########################

my @TASKS = (
    ['gateway', __('Firewall, HTTP proxy, traffic shaping, ...')],
    ['security', __('Firewall, content & mail filter, VPN, AV, IDS')],
    ['infrastructure', __('DHCP, DNS, NTP, web server, ...')],
    ['office', __('File sharing, printers, groupware')],
    ['communication', __('Mail, mail filter, VoIP, instant messaging')],
);

my @PACKAGES = (
    ['network', __('Networking')],
    ['firewall', __('Firewall and redirections')],
    ['trafficshaping', __('Traffic shaping')],
    ['samba', __('File sharing, PDC')],
    ['dns', __('Domain Name Service')],
    ['dhcp', __('DHCP server')],
    ['squid', __('HTTP Proxy and content filter')],
    ['usersandgroups', __('LDAP users and groups management')],
    ['monitor', __('Monitor CPU/mem/... with graphs & alerts')],
    ['software', __('System and eBox package managmenet')],
    ['ntp', __('Network Time Protocol server')],
    ['printers', __('Printing service')],
    ['jabber', __('XMPP server')],
    ['openvpn', __('VPN server')],
    ['webserver', __('Web Server')],
    ['mail', __('Mail system: SMTP, POP3, IMAP')],
    ['mailfilter', __('Antispam, Antivirus, transparent POP proxy')],
    ['antivirus', __('Antivirus')],
    ['egroupware', __('Calendar sharing, address book, webmail')],
    ['ids', __('Intrusion Detection System')],
#    ['captiveportal', __('Captive Portal')],
    ['asterisk', __('VoIP and PBX')],
#    ['improxy', __('Instant Messaging Proxy')],
);

#################
# -- constants --
#################

my $SELECTION_FILE = 'selection';
my $INSTALL_FILES = '/var/tmp';

my $INSTALLER_TITLE = __('eBox Installer');
my $SIMPLE_METHOD = __('simple');
my $SIMPLE_DESC = __('Select sets of packages');
my $ADVANCED_METHOD = __('advanced');
my $ADVANCED_DESC = __('Select packages manually');
my $TASK_TITLE = __('Select eBox tasks to install');
my $TASK_MESSAGE = __('Choose a profile');
my $PACKAGE_TITLE = __('Select eBox packages to install');
my $PACKAGE_MESSAGE = __('Choose eBox packages individually');
my $IFACES_TITLE = __('Select external interfaces');
my $IFACES_MESSAGE = __('Strict default policies are applied to incoming traffic through external interfaces');
my $PASS_TITLE = __('Enter password for eBox interface');
my $PASS_MSG = __('You will need to use this password to log into the eBox interface');
my $PASS_CONFIRM_TITLE = __('Confirm password for eBox interface');
my $PASS_CONFIRM_MSG = '';
my $MAIL_TITLE = __('Mail configuration');
my $MAIL_MESSAGE = __('Enter default virtual mail domain');
my $FINAL_TITLE = __('Installation finished');
my $FINAL_MESSAGE = __('Now you can access the eBox interface at ' .
                       'https://$YOUR_IP/');

my $LOG = ($ENV{LOG} or '/tmp/ebox-installer.log');

my $WHIPTAIL_ARGS = "--clear --backtitle \"$INSTALLER_TITLE\"";

########################
# -- global variables --
########################

my $CNF_password;
my @CNF_selectedIfaces;
my $CNF_domain = undef;

############################
# -- installation process --
############################

my @packages = selectPackagesMenu();
@packages = map ("ebox-$_", @packages);
installPackages(@packages);

# Apache is the most basic module, if it's not installed correctly,
# we assume that the installation has failed
if (system ("$INSTALL_FILES/ebox-module-installed apache") != 0) {
        showDialog(__('Error'), __('Installation failed.') . "\n\n" .
                   __("Check contents of $LOG to see what happened"));
        exit (1);
}

# Choose password for eBox interface
$CNF_password = askPassword();

# Choose interfaces to mark as external if ebox-network is installed
if (system ("$INSTALL_FILES/ebox-module-installed network") == 0) {
    @CNF_selectedIfaces = selectIfacesMenu();
}

# Choose mail domain if mail module is installed
if (system ("$INSTALL_FILES/ebox-module-installed mail") == 0) {
    $CNF_domain = askMailDomain();
}

preconfiguration();

# Show instalation complete message with access instructions
showDialog($FINAL_TITLE, $FINAL_MESSAGE);

###########################
# -- installer functions --
###########################

sub installPackages # (packages)
{
    my (@packages) = @_;

    my $packageList = join (' ', @packages);

    # Get number of packages to install
    my $num = `LANG=C apt-get install -s $packageList | grep ^Inst | wc -l`;
    chomp($num);

    my $aptCmd = 'LANG=C apt-get install -y --force-yes';
    my $message = __('Installing eBox packages...');
    my $whipCmd = "whiptail $WHIPTAIL_ARGS --gauge \"$message\" 7 64 0";
    my $gaugeCmd = "$INSTALL_FILES/ebox-apt-parse $num | $whipCmd";

    $ENV{DEBIAN_FRONTEND} = 'noninteractive';
    my $ret = system ("$aptCmd $packageList 2>&1 | tee -a $LOG | $gaugeCmd");

    if ($ret != 0) {
        showDialog('Error', 'Installation failed. ' .
                   "Check contents of $LOG to see what happened");
        exit (1);
    }
}

sub selectPackagesMenu # returns array
{
    my @selection;
    my $option = '';
    my $ret = 0;
    do {
        unless ($ret == -1) {
            $option = showMenu();
        }
        if ($option eq $SIMPLE_METHOD) {
            $ret = showChecklist($TASK_TITLE, $TASK_MESSAGE, @TASKS);
        } else {
            $ret = showChecklist($PACKAGE_TITLE, $PACKAGE_MESSAGE, @PACKAGES);
        }
        if ($ret == 0) {
            @selection = arrayFromFile($SELECTION_FILE);
            unlink ($SELECTION_FILE);
            if (scalar (@selection) == 0) {
                showDialog(__('Error'),
                           __('You must select at least one package.'));
                $ret = -1;
            }
        }
    } while ($ret != 0);

    return @selection;
}

sub selectIfacesMenu # returns array
{
    my $ifaceList = `$INSTALL_FILES/ebox-get-ifaces`;
    my @ifaces = split (' ', $ifaceList);
    my @ifacePairs = map ( [ $_, '' ] , @ifaces);

    my @selection;
    my $option = '';
    my $ret = 0;
    do {
        $ret = showChecklist($IFACES_TITLE, $IFACES_MESSAGE, @ifacePairs);
        if ($ret == 0) {
            @selection = arrayFromFile($SELECTION_FILE);
            unlink ($SELECTION_FILE);
        }
    } while ($ret != 0);

    return @selection;
}

sub askPassword # returns string
{
    my $password;
    my $confirm;
    do {
        $password = showPasswordDialog($PASS_TITLE, $PASS_MSG);
        $confirm = showPasswordDialog($PASS_CONFIRM_TITLE, $PASS_CONFIRM_MSG);

        if ($password ne $confirm) {
            showDialog(__('Error'), __("Passwords don't match"));
        }
    } until ($password eq $confirm);

    return $password;
}

sub askMailDomain # returns string
{
    my $domain;
    do {
        $domain = showInputDialog($MAIL_TITLE, $MAIL_MESSAGE);

        # TODO: domain validation?
    } until ($domain);

    return $domain;
}

sub preconfiguration
{
    # Workaround buggy squid without FQDN
    if (-f '/etc/squid/squid.conf') {
        system('echo "visible_hostname localhost" >> /etc/squid/squid.conf');
        system('/etc/init.d/squid stop > /dev/null 2>&1');
        system('/etc/init.d/squid start > /dev/null 2>&1');
   }

    my $message = __('Preconfiguring eBox packages...');
    my $whipCmd = "whiptail $WHIPTAIL_ARGS --gauge \"$message\" 7 64 0";
    my $PROGRESS;
    open ($PROGRESS, '|-', $whipCmd);

    # Enable all eBox installed modules

    my $moduleList = `$INSTALL_FILES/ebox-enable-list`;
    print $PROGRESS "1\n";

    chomp($moduleList);
    my @modules = split (/\n/, $moduleList);
    my $numModules = scalar (@modules);
    if ($numModules > 0) {
        my $step = 40 / $numModules;
        my $counter = $step;
        foreach my $module (@modules) {
            system("$INSTALL_FILES/ebox-enable-module $module >> $LOG 2>&1");
            print $PROGRESS int($counter) . "\n";
            $counter += $step;
        }

        system("$INSTALL_FILES/ebox-update-digests >> $LOG 2>&1");
        $counter = 45;
        print $PROGRESS "$counter\n";

        # save changes
        foreach my $module (@modules) {
            system("$INSTALL_FILES/ebox-save-module $module >> $LOG 2>&1");
            print $PROGRESS int($counter) . "\n";
            $counter += $step;
        }
    }


    # load LANG variable with default locale
    my @lines = arrayFromFile('/etc/default/locale');
    my $LANG = (split ('"', $lines[0]))[1];
    $ENV{LANG} = $LANG;

    print $PROGRESS "86\n";

    # Append eBox support languages to generate to current supported
    # locales
    my $LOCALES_FILE='/var/lib/locales/supported.d/local';
    my $TMP='/tmp/local.tmp';
    system("cat /var/tmp/locale.gen $LOCALES_FILE > $TMP");
    system("sort $TMP | uniq > $LOCALES_FILE");
    unlink($TMP);

    print $PROGRESS "87\n";

    # Regenerate locales to update the new messages from eBox
    system("/usr/sbin/locale-gen >> $LOG 2>&1");

    print $PROGRESS "96\n";

    system("/usr/share/ebox/ebox-set-locale $LANG >> $LOG 2>&1");

    print $PROGRESS "97\n";

    # Run ebox-software in order to update packages list (which is done
    # nightly)
    #system('ebox-software');

    # eBox initial setup
    system ("$INSTALL_FILES/ebox-set-password $CNF_password >> $LOG 2>&1");

    print $PROGRESS "98\n";

    foreach my $iface (@CNF_selectedIfaces) {
        system ("$INSTALL_FILES/ebox-set-external $iface >> $LOG 2>&1");
    }

    print $PROGRESS "99\n";

    if (defined $CNF_domain) {
        system ("$INSTALL_FILES/ebox-set-maildomain $CNF_domain >> $LOG 2>&1");
    }

    print $PROGRESS "100\n";

    close ($PROGRESS);
}

#################################
# -- whiptail helper functions --
#################################

sub showMenu
{
    my $title = __('Choose package selection method');
    my $message = __('Select the method that better fits your needs');

    my $size = 2;
    my $height = $size + 7;

    my $arg1 = "$SIMPLE_METHOD \"$SIMPLE_DESC\"";
    my $arg2 = "$ADVANCED_METHOD \"$ADVANCED_DESC\"";

    my $max_len = max(length($arg1), length($arg2), length ($title));
    my $width = 7 + $max_len;

    my $command = "whiptail $WHIPTAIL_ARGS --nocancel --title \"$title\" " .
                  "--menu \"$message\" $height $width $size $arg1 $arg2";

    my $file = 'selected_option';
    system ("$command 2> $file");
    my @lines = arrayFromFile($file);
    unlink ($file);

    return $lines[0];
}

sub showChecklist # (title, message, options)
{
    my ($title, $message, @options) = @_;

    my $size = scalar (@options);
    if ($size > 14) {
        $size = 14;
    }
    my $height = $size + 7;

    my $option_args;
    my $max_len = 0;
    foreach my $optionPair (@options) {
        my $option = $optionPair->[0];
        my $description = $optionPair->[1];
        my $arg = "$option \"$description\"";
        my $this_len = length ($arg) + 8;
        if ($this_len > $max_len) {
            $max_len = $this_len;
        }
        $option_args .= "$arg 0 ";
    }
    my $width = max(get_dialog_width($title, $message), $max_len + 10);
    my $max_chars = max(length($title), length($message), $max_len);
    if ($max_chars > 70) {
        $height += int($max_chars / 70);
    }

    my $command = "whiptail $WHIPTAIL_ARGS --separate-output " .
                  "--title \"$title\" " .
                  "--checklist \"$message\" $height $width $size $option_args";

    system ("$command 2> $SELECTION_FILE");
}

sub showInputDialog # (title, message)
{
    my ($title, $message) = @_;

    showGenericInputDialog('inputbox', $title, $message);
}

sub showPasswordDialog # (title, message)
{
    my ($title, $message) = @_;

    showGenericInputDialog('passwordbox', $title, $message);
}

sub showGenericInputDialog # (type, title, message) returns input string
{
    my ($type, $title, $message) = @_;

    my $height = 8;
    my $width = get_dialog_width($title, $message);

    my $command = "whiptail $WHIPTAIL_ARGS --separate-output " .
                  "--title \"$title\" " .
                  "--$type \"$message\" $height $width";

    my @selection;
    do {
        system ("$command 2> $SELECTION_FILE");
        @selection = arrayFromFile($SELECTION_FILE);
        unlink ($SELECTION_FILE);
    } until (scalar(@selection) > 0);

    return $selection[0];
}

sub showDialog # (title, message)
{
    my ($title, $message) = @_;

    my $size = 1 + int(length($message) / 70);
    my $height = $size + 7;
    my $width = get_dialog_width($title, $message);

    my $command = "whiptail $WHIPTAIL_ARGS --title \"$title\" " .
                  "--msgbox \"$message\" $height $width";

    system ($command);
}

##############################
# -- other helper functions --
##############################

sub arrayFromFile # (filename)
{
    my ($filename) = @_;

    my $fh;
    open ($fh, '<', "$INSTALL_FILES/$filename") or
        open ($fh, '<', $filename) or
            die "Can't read $filename file";
    my @array = <$fh>;
    chomp (@array);
    close ($fh);

    return @array;
}

sub get_dialog_width # (title, message) returns int
{
    my ($title, $message) = @_;

    my $width = 7 + length($title);
    if (length($message) > length($title)) {
        if (length($message) < 70) {
            $width = length($message) + 7;
        } else {
            $width = 70;
        }
    }

    return $width;
}

sub max # (val1, val2, ...) returns value
{
    my $max = 0;
    map { $max = $_ if ($_ > $max) } @_;
    return $max;
}

